plugins {
    id "java"
    id "java-library"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "io.freefair.maven-optional" version "5.0.0-rc6"
    id "maven-publish"
    id "signing"
}

group = "com.github.amuguelove"
version = "1.0.0"
description = "fast integrate p6spy for Spring Boot"

ext {
    set('springBootVersion', '2.2.5.RELEASE')
    set('p6spyVersion', '3.8.7')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) { options.encoding = 'utf-8' }

dependencies {
    api("p6spy:p6spy:${p6spyVersion}")
    optional("org.springframework.boot:spring-boot-starter")
    optional("org.springframework.boot:spring-boot-autoconfigure")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

javadoc {
    description = "生成jar格式的javadoc。"
    // 只显示 protected 和 public 的类和成员
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.author = true
    options.version = true
    options.header = project.name
    // 静默javadoc检查（比如不支持@date会报错等），jdk 8+
    options.addStringOption('Xdoclint:none', '-quiet')
    /**
     * <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
     * 防止本地打开中文乱码
     */
    options.addStringOption("charset", "UTF-8")
    // suppress warnings due to cross-module @see and @link references;
    // note that global 'api' task does display all warnings.
    logging.captureStandardError LogLevel.INFO
    // suppress "## warnings" message
    logging.captureStandardOutput LogLevel.INFO
    // 编码一定要配置否则直接出错
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.amuguelove'
            artifactId = 'p6spy-spring-boot-autoconfigure'
            version = '1.0.0'
            from components.java

            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'p6spy-spring-boot-autoconfigure'
                description = 'quickly integrate p6spy for spring boot'
                url = 'https://github.com/amuguelove/spring-boot-projects'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                        comments = 'A business-friendly OSS license'
                    }
                }
                developers {
                    developer {
                        name = 'liangbo'
                        email = 'bo.liang0212@gmail.com'
                        roles = ['Java Developer']
                        url = "https://amuguelove.github.io"
                    }
                }
                scm {
                    url = 'https://github.com/amuguelove/spring-boot-projects'
                    connection = 'scm:git:https://github.com/amuguelove/spring-boot-projects.git'
                    developerConnection = 'scm:git:git@github.com:amuguelove/spring-boot-projects.git'
                }
            }
        }
    }

    repositories {
        maven {
            name "oss"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}